/*
 * Copyright 2014 Meetup
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:0.10.+'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
}

group = 'com.meetup.gradle'
version = '1.1'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'api-gradle-plugins'
                    description 'Gradle plugins for interacting with the Bugsense and Phraseapp APIs'
                    url 'https://github.com/meetup/api-gradle-plugins'
                    inceptionYear '2014'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    scm {
                        url 'https://github.com/meetup/api-gradle-plugins'
                        connection 'scm:git:https://github.com/meetup/api-gradle-plugins.git'
                        developerConnection 'scm:git:ssh://git@github.com:meetup/api-gradle-plugins.git'
                    }
                    developers {
                        developer {
                            id 'mlc'
                            name 'Mike Castleman'
                            email 'mlc@meetup.com'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        // userOrg = 'meetup'
        name = 'api-gradle-plugins'
        desc = 'Gradle plugins for interacting with the Bugsense and Phraseapp APIs'
        licenses = ['Apache-2.0']
    }
    dryRun = false
}
